
;                                   FILE 7 prn and expression analyser > DBAS06

FCOMPR JSR PHFACC
 JSR ADDER
 TAY
 JSR FLOATI
 JSR POPSET
FCMP JSR FLDW
 LDA FACCS
 EOR FWRKS
 BPL FCMPA ;X preserved from ADDER to here
 ASL FACCS
 BRA FCMPZY-2
FCOMPT STY IACCH
 PLA
 STA IACCN
 PLA
 STA IACCM
 PLA
 STA IACCL
 JSR FNEG ;factors -ve & reversed
 JSR IFLT-3 ;FTOW & IFLT
FCMPA LDA FWRKX
 CMP FACCX
 BNE FCMPZZ
 LDA FWRKMA
 CMP FACCMA
 BNE FCMPZZ
 LDA FWRKMB
 CMP FACCMB
 BNE FCMPZZ
 LDA FWRKMC
 CMP FACCMC
 BNE FCMPZZ
 LDA FWRKMD
 SBC FACCMD
 BEQ FCMPZY
FCMPZZ RORA
 EOR FWRKS
 ROLA
 LDAIM -1
FCMPZY RTS ;X preserved from ADDER to here
COMPRE JMP LETM
 INC AECUR
COMPR TXA  ;string or numeric compare
 BEQ STNCMP
 BMI FCOMPR
 LDA IACCL
 PHA
 LDA IACCM
 PHA
 LDA IACCN
 PHA
 LDA IACCH
 PHA
 JSR ADDER
 TAY
 BMI FCOMPS
 BEQ COMPRE
 PLA
 TAY
 EOR IACCH
 BMI COMPOS ;opposite signs
 CPY IACCH
 BNE COMPOT
 PLA
 CMP IACCN
 BNE COMPOT+1
 PLA
 CMP IACCM
 BNE COMPOT+2
 PLA
 SBC IACCL
 BNE COMPOT+3
 RTS
FCOMPS PLA
 TAY
 EOR FACCS
 BPL FCOMPT
COMPOS TYA
 EORIM -1
 ASLA
COMPOT PLA
 PLA
 PLA
COMPOU LDAIM -1
 RTS
STNCMP JSR PHSTR
 JSR STREXP
 LDAI AESTKP ;other clen
 CMP CLEN
 BCC COMPRF
 LDA CLEN
COMPRF STA WORK
COMPRG CPY WORK ;Y was zero
 BEQ COMPRH
 INY
 LDAIY AESTKP
 CMPAY STRACC-1
 BEQ COMPRG
 BRA COMPRI
COMPRH LDAI AESTKP
 CMP CLEN
COMPRI PHP
 JSR POPSTX ;doesn't use X
 PLP
 BNE COMPOU
 LDAIM 0
 RTS ;has preserved X throughout
AEEXPR LDA LINE
 STA AELINE
 LDA LINE+1
 STA AELINE+1
 LDA CURSOR
 STA AECUR
;  EXPR reads a rhs. If status is eq then it returned a string.
;  If status is neq and plus then it returned a word.
;  If status is neq and minus then it returned an fp.
EXPR JSR ANDER
EXPRQ CPXIM TOR
 BEQ OR
 CPXIM TEOR
 BEQ EOR
 DEC AECUR
 TAY
 STA TYPE
 RTS
OR JSR PHANDR
 JSR INTEGY
 LDYIM 3
ORLP LDAIY AESTKP
 ORAAY IACCL
 STAAY IACCL
 DEY
 BPL ORLP
EXPRP JSR POPINC
 LDAIM &40
 BRA EXPRQ
EOR JSR PHANDR
 JSR INTEGY
 LDYIM 3
EORLP LDAIY AESTKP
 EORAY IACCL
 STAAY IACCL
 DEY
 BPL EORLP
 BRA EXPRP
PHANDR JSR INTEGY
 JSR PHACC
ANDER JSR RELATE
ANDERQ CPXIM TAND
 BEQ AND
 RTS
AND JSR INTEGY
 JSR PHACC
 JSR RELATE
 JSR INTEGY
 LDYIM 3
ANDLP LDAIY AESTKP
 ANDAY IACCL
 STAAY IACCL
 DEY
 BPL ANDLP
 JSR POPINC
 LDAIM &40
 BRA ANDERQ
RELATE JSR ADDER
 CPXIM "?"
 BCS RELATX
 CPXIM "<"
 BCS RELTS
RELATX RTS
RELTS BEQ RELTLT
 CPXIM ">"
 BEQ RELTGT
 TAX ;test equal
 JSR COMPR+1
OTHER EORIM -1
SAME STA IACCL
 STA IACCM
 STA IACCN
 STA IACCH
 LDAIM &40 ;type integer
 RTS
RELTLT TAX
 LDY AECUR
 LDAIY AELINE
 CMPIM "="
 BEQ LTOREQ
 CMPIM ">"
 BEQ NEQUAL
 JSR COMPR ;test less than
 BCC SAME
 BEQ SAME
 BRA OTHER
LTOREQ JSR COMPR-2
 BCC SAME
 BRA OTHER
NEQUAL JSR COMPR-2
 BRA SAME
RELTGT TAX
 LDY AECUR
 LDAIY AELINE
 CMPIM "="
 BEQ GTOREQ
 JSR COMPR ;test greater than
 BCS SAME
 BRA OTHER
GTOREQ JSR COMPR-2
 BCC OTHER
 BEQ OTHER
 BRA SAME
STROVR BRK
 = &13
 = "String too long"
 BRK
STREXP JSR POWER
 TAY
 BNE ADDERE
 CPXIM "+"
 BNE PLUS-1
STNCON JSR PHSTR
 JSR POWER
 TAY
 BNE ADDERE
 CLC
 PHX
 LDAI AESTKP
 ADC CLEN
 BCS STROVR
 TAX
 PHA
 LDY CLEN
CONLOP LDAAY STRACC-1
 STAAX STRACC-1
 DEX
 DEY
 BNE CONLOP
 JSR POPSTR
 PLA
 STA CLEN
 PLX
 LDAIM 0
 BRA STNCON-4
ADDER JSR TERM
ADDERQ CPXIM "+"
 BEQ PLUS
 CPXIM "-"
 BEQ MINUS
 RTS
PLUS TAY
 BMI FPLUS
 BEQ STNCON
 JSR PHTERM
 TAY
 BMI FPLUST
 BEQ ADDERE
 CLC
 LDAI AESTKP
 ADC IACCL
 STA IACCL
 LDYIM 1
 LDAIY AESTKP
 ADC IACCM
 STA IACCM
 INY
 LDAIY AESTKP
 ADC IACCN
 STA IACCN
 INY
 LDAIY AESTKP
 ADC IACCH
ADDERP STA IACCH
 CLC
 LDA AESTKP
 ADCIM 4
 STA AESTKP
 LDAIM &40 ;reset type
 BCC ADDERQ
 INC AESTKP+1
 BRA ADDERQ
ADDERE JMP LETM
FPLUS JSR FADSUB
 JSR FADD
FFFFA LDX TYPE
 BRA ADDERQ
MINUS TAY
 BMI FMINUS
 BEQ ADDERE
 JSR PHTERM
 TAY
 BMI FMINUT
 BEQ ADDERE
 SEC
 LDAI AESTKP
 SBC IACCL
 STA IACCL
 LDYIM 1
 LDAIY AESTKP
 SBC IACCM
 STA IACCM
 INY
 LDAIY AESTKP
 SBC IACCN
 STA IACCN
 INY
 LDAIY AESTKP
 SBC IACCH
 BRA ADDERP
FMINUS JSR FADSUB 
 JSR FXSUB
 BRA FFFFA
FMINUT JSR FNEG
FPLUST STX TYPE
 JSR FWPPFL ;POPACC,FTOW &IFLT
 LDA FWRKMA
 JSR FADD+3
 BRA FFFFA
FTIMLF JSR IFLT
FTIML JSR FWPPFL
 AND FWRKMA
 JSR FMUL1
 BRA FTIMR
FTIMFL JSR IFLT
FTIM JSR PHFACC
 JSR POWER
 JSR FLPPST
 JSR FMUL
FTIMR BRA TERMQ ;X has been preserved to here
TIMES TAY
 BMI FTIM
 BEQ ADDERE
 LDA IACCM
 ASLA
 LDA IACCN
 ADCIM 0
 BNE FTIMFL
 ADC IACCH
 BNE FTIMFL
 JSR PHPOW
 TAY
 BMI FTIML
 BEQ TIMES+3
 LDA IACCM
 STA WORK+1
 ASLA
 LDA IACCN
 ADCIM 0
 BNE FTIMLF
 ADC IACCH
 BNE FTIMLF
 BCC .+5
 JSR COMPNO
 STX TYPE
 LDXIM WORK+6
 JSR ACCTOM
 JSR POPACC
 LDYIM 0
 LDXIM 0
NUL LSR WORK+7
 ROR WORK+6
 BCC NAD
 CLC
 TYA
 ADC IACCL
 TAY
 TXA
 ADC IACCM
 TAX
 LDA WORK+8
 ADC IACCN
 STA WORK+8
 LDA WORK+9
 ADC IACCH
 STA WORK+9
NAD ASL IACCL
 ROL IACCM
 ROL IACCN
 ROL IACCH
 LDA WORK+6
 ORA WORK+7
 BNE NUL
 ASSERT :MSB: NUL = :MSB: .
 STY WORK+6
 STX WORK+7
 BRA REMAIN+3
PHTERM JSR PHACC
TERM JSR POWER
TERMQ CPXIM "*"
 BEQ TIMES
 CPXIM "/"
 BCC DIVIDE-1
 BEQ DIVIDE
 CPXIM TMOD
 BEQ REMAIN
 CPXIM TDIV
 BEQ INTDIV
 RTS
DIVIDE JSR FLPHFA
 JSR POWER
 JSR FLPPST-2
 JSR FXDIV
 BRA TERMA
REMAIN JSR DIVOP
 LDA WORK+1
 PHP
REMIN LDXIM WORK+6
DIVIN JSR MTOACC
 PLP
 BPL TERMA
 JSR COMPNO
TERMA LDX TYPE
 BRA TERMQ
INTDIV JSR DIVOP
 BIT WORK
 PHP
 LDXIM WORK+2
 BRA DIVIN
PHPOW JSR PHACC
POWER JSR FACTOR
POWERB PHA
POWERA LDY AECUR
 INC AECUR
 LDAIY AELINE
 CMPIM " "
 BEQ POWERA
 TAX
 PLA
 CPXIM "^"
 BEQ POW
 RTS
POW JSR FLPHFA
 JSR FACTOR
 BMI FPOWG+3 ;REAL Exponent
 BEQ POW ;no return
 LDA IACCL
 ASLA
 LDAIM 0
 ADC IACCM
 BNE FPOWG
 ADC IACCN
 BNE FPOWG ;Too big, treat as REAL
 ADC IACCH
 BEQ FPOWI
FPOWG JSR IFLT
 LDA FACCX
 BEQ FPOWI-2 ;n=0 or near enough
 CMPIM &87
 BCS FPOWJ ;n>=64
 CMPIM &81
 BCC FPOWJ ;n<1
 ORAIM &78
 TAY
 LDA FACCMB
 ORA FACCMC
 ORA FACCMD
 BNE FPOWJ ;fractional parts
 LDA FACCMA
 LSRA
 BCS FPOWJ
 INY
 BMI .-4
 LDY FACCS
 BPL FPOWI-2
 EORIM &FF
 INA
 STA IACCL
FPOWI JSR FLDSTK ;FLD from stack
 LDA IACCL
 JSR FIPOW
 BRA POWERB
FPOWJ LDAIM FWSB
 JSR FSTAP
 JSR FLDSTK
 JSR FLOG
 LDAIM FWSB
 LDYIM /FWSB
 JSR CX+2
 JSR FEXP
 BRA POWERB
POSITE LDAIM 0
 BRA NPRN+2
NPRN LDAIM 5 ;print number to chout 5 size in decimal
 STA PRINTS
 LDXIM 4
NUMLOP CLRAX WORK+8
 SEC
NUMLP LDA IACCL
 SBCAX VALL
 TAY
 LDA IACCM
 SBCAX VALM
 BCC OUTNUM
 STA IACCM
 STY IACCL
 INCAX WORK+8
 BRA NUMLP
OUTNUM DEX
 BPL NUMLOP
 LDXIM 5
LZB DEX
 BEQ LASTZ
 LDAAX WORK+8
 BEQ LZB
LASTZ STX WORK
 LDA PRINTS
 BEQ PLUME
 SBC WORK ;carry clear
 BEQ PLUME
 TAX
 JSR LISTPL
 LDX WORK
PLUME LDAAX WORK+8
 ORAIM &30
 JSR CHOUT
 DEX
 BPL PLUME
 RTS
 LNK DBAS07
