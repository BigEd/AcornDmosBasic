;                                 FILE F input read line editor > DBAS12
;                                        stack files

INPUHD JMP LETM
INPUHE JMP STDED
INPUHX STY CURSOR
INPUHZ JMP DONEXT
INPUTH JSR DAECHAN
 STY COEFP
 JSR ASCUR
INPUHL JSR CHKCOM
 BNE INPUHX
 LDA COEFP
 PHA
 JSR CRAELV
 BEQ INPUHE
 JSR ASCUR
 PLA
 STA COEFP
 PHP
 JSR PHACC
 LDY COEFP
 JSR OSBGET
 STA TYPE
 PLP
 BCC INPUHN
 LDA TYPE
 BNE INPUHD
 JSR OSBGET
 STA CLEN
 TAX
 BEQ INPUHS
INPUHT JSR OSBGET
 STAAX STRACC-1
 DEX
 BNE INPUHT
INPUHS JSR STSTOR
 BRA INPUHL
INPUHN LDA TYPE
 BEQ INPUHD
 BMI INPUHF
 LDXIM 3
INPUHI JSR OSBGET
 STAAX IACCL
 DEX
 BPL INPUHI
 BRA INPUHJ
INPUHF LDXIM 4
INPUHR JSR OSBGET
 STAAX FWSA
 DEX
 BPL INPUHR
 JSR LDARGA
INPUHJ JSR POPWRK
 JSR STORF
 BRA INPUHL
INOUT PLA
 PLA
 BRA INPUHZ
INPUT JSR CHKHSH
 BEQ INPUTH
 CMPIM TLINE
 BEQ INLIN
 DEC CURSOR
 CLC
INLIN ROR COEFP
 LSR COEFP
 LDAIM &FF
 STA COEFP+1
INPLP JSR PRTSTN
 BCS INPHP
INPLO JSR PRTSTN
 BCC INPLO
 LDXIM &FF
 STX COEFP+1
 CLC
INPHP PHP
 ASL COEFP
 PLP
 ROR COEFP
 CMPIM ","
 BEQ INPLP
 CMPIM ";"
 BEQ INPLP
 DEC CURSOR
 LDA COEFP
 PHA
 LDA COEFP+1
 PHA
 JSR CRAELV
 BEQ INOUT
 PLA
 STA COEFP+1
 PLA
 STA COEFP
 JSR ASCUR
 PHP
 BIT COEFP
 BVS INGET
 LDA COEFP+1
 CMPIM &FF
 BNE INGOT
INGET BIT COEFP
 BPL INGETA
 LDAIM "?"
 JSR OSWRCH
INGETA JSR INLINE
 STY CLEN ;A=0
 ASL COEFP
 CLC
 ROR COEFP
 BIT COEFP
 BVS INGETB
INGOT STA AECUR
 CLR AELINE ;STRACC=0
 LDAIM /STRACC
 STA AELINE+1
 JSR DATAST
INTERM JSR COMCHK
 BEQ INGETC
 CMPIM &0D
 BNE INTERM
 LDYIM &FE
INGETC INY
 STY COEFP+1
INGETB PLP
 BCS INPSTR
 JSR PHADDR
 JSR VALSTR
 JSR STORE
INPLPJ BRA INPLP
INPSTR CLR TYPE
 JSR STSTRE
 BRA INPLPJ
RESTORE CLR WORK+6
 LDY TXTP
 STY WORK+7
 JSR SPACES
 DEC CURSOR
 CMPIM ":"
 BEQ RESDON
 CMPIM &0D
 BEQ RESDON
 CMPIM TELSE
 BEQ RESDON
 JSR GOFACT
RESDON JSR DONE
 LDA WORK+6
 STA DATAP
 LDA WORK+7
 STA DATAP+1
 JMP NXT
READS JSR CHKCOM
 BEQ READ
 JMP SUNK
READ JSR CRAELV
 BEQ READS
 BCS READST
 JSR DATAIT ;read number
 JSR PHADDR
 JSR STEXPR
 BRA READEN
READST JSR DATAIT
 JSR PHACC
 JSR DATAST
 STA TYPE
 JSR STSTOR
READEN CLC
 LDA AECUR
 ADC AELINE
 STA DATAP
 LDA AELINE+1
 ADCIM 0
 STA DATAP+1
 BRA READS
DATAIT JSR ASCUR
 LDA DATAP
 STA AELINE
 LDA DATAP+1
 STA AELINE+1
 CLR AECUR
 JSR COMCHK
 BEQ DATAOK
 CMPIM TDATA
 BEQ DATAOK
 CMPIM &0D
 BEQ DATANX
DATALN JSR COMCHK
 BEQ DATAOK
 CMPIM &0D
 BNE DATALN
DATANX LDY AECUR
 LDAIY AELINE
 BMI DATAOT
 INY
 INY
 LDAIY AELINE
 TAX
DATANS INY
 LDAIY AELINE
 CMPIM " "
 BEQ DATANS
 CMPIM TDATA
 BEQ DATAOL
 TXA
 CLC
 ADC AELINE
 STA AELINE
 BCC DATANX
 INC AELINE+1
 BRA DATANX
DATAOT BRK
 = &2A
 = "Out of ",TDATA
NODOS BRK
 = &2B
 = "No ",TREPEAT
CHANNE BRK
 = &2D
 = &8D,"#"
DODP BRK
 = &2C
 = "Too many ",TREPEAT,"s"
 BRK
DATAOL INY
 STY AECUR
DATAOK RTS
UNTIL JSR AEEXPR
 JSR FDONE
 JSR INTEG
 LDX DOSTKP
 BEQ NODOS
 LDA IACCL
 ORA IACCM
 ORA IACCN
 ORA IACCH
 BEQ REDO
 DEC DOSTKP
 JMP NXT
REDO LDYAX DOADL-1
 LDAAX DOADH-1
 JMP JUMPAY
DAECHAN DEC CURSOR
AECHAN LDA CURSOR
 STA AECUR
 LDA LINE
 STA AELINE
 LDA LINE+1
 STA AELINE+1
CHANN JSR AESPAC
 CMPIM "#"
 BNE CHANNE
 JSR INTFAC
 LDY IACCL
 TYA
 RTS
REPEAT LDX DOSTKP
 CPXIM DOTOP
 BCS DODP
 JSR CLYADP
 LDA LINE
 STAAX DOADL
 LDA LINE+1
 STAAX DOADH
 INC DOSTKP
 JMP STMT
INLINE LDAIM /STRACC
 BRA BUFFA
BUFF LDAIM /BUFFER
BUFFA CLR WORK ;both stracc and buffer=0
 STA WORK+1
 LDAIM &EE
 STA WORK+2
 LDAIM " "
 STA WORK+3
 LDYIM &FF
 STY WORK +04
 INY
 LDXIM WORK
 TYA
 JSR OSWORD
 BCC BUFEND
 JMP DOBRK
NLINE JSR OSCRLF
BUFEND CLR TALLY
 RTS
REMOVE JSR FNDLNO   ;removes line whose number is in IACCL
 BCC REMOVX
 LDA WORK+6
 STA WORK
 STA TOP
 LDA WORK+7
 STA WORK+1
 STA TOP+1
 LDYIM 3
 LDAIY WORK
 CLC
 ADC WORK
 STA WORK
 BCC MOVEA
 INC WORK+1
MOVEA LDYIM 0
 LDAIY WORK
 STAIY TOP
 CMPIM &0D
 BNE MOVL
 INY
 BNE MOV
 INC WORK+1
 INC TOP+1
MOV LDAIY WORK
 STAIY TOP
 BMI REMEND
 JSR SILINC
 JSR SILINC
MOVL INY
 BNE MOVEA+2
 INC WORK+1
 INC TOP+1
 BRA MOVEA+2
REMEND JMP YADT
SILINC INY
 BNE SILINX
 INC TOP+1
 INC WORK+1
SILINX LDAIY WORK
 STAIY TOP
REMOVX RTS
SUPERBIT LDXIM &FF
 STX BYTESM
 STX CONSTA
 JSR SETVAR
 LDA LINE
 STA WORK
 LDA LINE+1
 STA WORK+1
 CLR MODE
 CLR CURSOR
 JSR MATCHA
 JSR SPTSTN
 BCC REMOVX
INSRT LDA LISTOP
 BEQ INSRTS
INSRTL LDAAY BUFFER
 INY
 CMPIM " "
 BEQ INSRTL
 DEY
INSRTS STY WORK+4
 JSR REMOVE ;find old line and remove it (leaves work+6)
 LDYIM /BUFFER
 STY WORK+5
 LDYIM 0
 LDAIM &0D
 CMPI WORK+4
 BEQ REMOVX;carry set
LENGTH INY
 CMPIY WORK+4
 BNE LENGTH
 LDAIM " "
TRALSP DEY
 BEQ TRALEX
 CMPIY WORK+4
 BEQ TRALSP
TRALEX INY
 LDAIM &0D
 STAIY WORK+4
 INY
 INY
 INY
 INY
 STY WORK+8
 LDA TOP
 STA WORK+2
 LDA TOP+1
 STA WORK+3
 JSR CLYADT
 STA WORK
 LDA TOP+1
 STA WORK+1
 DEY
 LDA HIMEM
 CMP TOP
 LDA HIMEM+1
 SBC TOP+1
 BCS MOVEUP
 JSR ENDER ;restore TOP
 JSR SETFSA
 BRK
 BRK
 = TLINE," space"
 BRK
MOVEUP LDAIY WORK+2
 STAIY WORK
 TYA
 BNE LOW
 DEC WORK+3
 DEC WORK+1
LOW DEY
 TYA
 ADC WORK+2
 LDX WORK+3
 BCC LOWW
 INX
LOWW CMP WORK+6
 TXA
 SBC WORK+7
 BCS MOVEUP
 LDYIM 1
 LDA IACCM
 STAIY WORK+6
 INY
 LDA IACCL
 STAIY WORK+6
 INY
 LDA WORK+8
 STAIY WORK+6
 SEC
 TYA
 ADC WORK+6
 STA WORK+6
 BCC CLYIDW
 INC WORK+7
CLYIDW LDYIM &FF
INSLOP INY
 LDAIY WORK+4
 STAIY WORK+6
 CMPIM &0D
 BNE INSLOP
 RTS ;carry must be set at the end of INSRT
SETFSA LDA TOP
 STA LOMEM
 STA FSA
 LDA TOP+1
 STA LOMEM+1
 STA FSA+1
 JSR SETVAR
SETVAL LDXIM RPLN10-VECTAB
 LDAAX VECTAB-1
 STAAX BUFFER+256-(RPLN10-VECTAB)-1
 DEX
 BNE SETVAL+2
 LDXIM &80
SETVRL CLRAX VARPTR-1
 DEX
 BNE SETVRL
 RTS
SETVAR LDA TXTP
 STA DATAP+1
 LDA HIMEM
 STA AESTKP
 LDA HIMEM+1
 STA AESTKP+1
 LDAIM &80
 TRB LISTOP
 CLR DOSTKP
 CLR FORSTP
 CLR SUBSTP
 CLR DATAP
 RTS
FADSUB JSR PHFACC
 JSR TERM
 STX TYPE ;extra entry
FLPPST TAY ;floats and prepares for other argument
 JSR FLOATI
POPSET LDA AESTKP ;pop the stack and set argp to point to the entry
 CLC
 STA ARGP
 ADCIM &05
 STA AESTKP
 LDA AESTKP+1
 STA ARGP+1
 ADCIM 0
 STA AESTKP+1
 RTS
FLPHFA TAY ;floats and pushes into stack
 JSR FLOATI
PHFACC LDA AESTKP
 SEC
 SBCIM 5
 JSR HIDEC
 LDA FACCX
 STAI AESTKP
 LDYIM 1
 LDA FACCS
 EOR FACCMA
 ANDIM &80
 EOR FACCMA
 STAIY AESTKP
 INY
 LDA FACCMB
 STAIY AESTKP
 INY
 LDA FACCMC
 STAIY AESTKP
 INY
 LDA FACCMD
 STAIY AESTKP
 RTS
PHTYPE BEQ PHSTR
 BMI PHFACC
PHACC LDA AESTKP
 SEC
 SBCIM 4
 JSR HIDEC
 LDYIM 3
 LDA IACCH
 STAIY AESTKP
 DEY
 LDA IACCN
 STAIY AESTKP
 DEY
 LDA IACCM
 STAIY AESTKP
 LDA IACCL
 STAI AESTKP
 RTS
PHADDR PLY
 PLX
 LDA IACCL
 PHA
 LDA IACCM
 PHA
 LDA IACCN
 PHA
 PHX
 PHY
 RTS
STRCOM JMP COMERR
STCMPH JSR STREXP
 CPXIM ","
 BNE STRCOM
PHSTR CLC
 LDA AESTKP
 SBC CLEN
 JSR HIDEC
 LDY CLEN
 BEQ PHSTRX
PHSTRL LDAAY STRACC-1
 STAIY AESTKP
 DEY
 BNE PHSTRL
PHSTRX LDA CLEN
 STAI AESTKP
 RTS
STORST LDA WORK+2
 CMPIM &80
 BEQ STORSX
 BCC STORIT
 LDAI AESTKP
 TAX
 BEQ STORSY
 LDAI WORK
 SBCIM 1
 STA WORK+2
 LDYIM 1
 LDAIY WORK
 SBCIM 0
 STA WORK+3
STORSL LDAIY AESTKP
 STAIY WORK+2
 INY
 DEX
 BNE STORSL
STORSY LDAI AESTKP
 LDYIM 3
STORSW STAIY WORK
 BRA POPSTX
STORSX LDAI AESTKP
 TAX
 BEQ STORSZ
 LDYIM 1
STORSV LDAIY AESTKP
 DEY
 STAIY WORK
 INY
 INY
 DEX
 BNE STORSV
STORSZ LDAIM &0D
 BNE STORSW
STORIT LDAI AESTKP
 STAI WORK
 LDYIM 4
 LDA WORK+2
 BEQ STORIY
 LDYIM 1
 LDAIY AESTKP
 STAIY WORK
 INY
 LDAIY AESTKP
 STAIY WORK
 INY
 LDAIY AESTKP
 STAIY WORK
 INY
 CPY WORK+2
 BCS STORIY
 LDAIY AESTKP
 STAIY WORK
 INY
STORIY TYA
 CLC
 BRA POPN
POPSTR LDAI AESTKP
 STA CLEN
 BEQ POPSTY
 TAY
POPSTL LDAIY AESTKP
 STAAY STRACC-1
 DEY
 BNE POPSTL
POPSTX LDAI AESTKP
POPSTY SEC
 BRA POPN
POPACC LDYIM 3
 LDAIY AESTKP
 STA IACCH
 DEY
 LDAIY AESTKP
 STA IACCN
 DEY
 LDAIY AESTKP
 STA IACCM
 LDAI AESTKP
 STA IACCL
POPINC CLC
 LDAIM 4
POPN ADC AESTKP
 STA AESTKP
 BCC POPACI
 INC AESTKP+1
POPACI RTS
POPWRK LDXIM WORK
POPX LDYIM 3
 LDAIY AESTKP
 STAAX 3
 DEY
 LDAIY AESTKP
 STAAX 2
 DEY
 LDAIY AESTKP
 STAAX 1
 LDAI AESTKP
 STAAX 0
 BRA POPINC
HIDEC STA AESTKP
 BCS HIDECA
 DEC AESTKP+1
HIDECA LDY AESTKP+1
 CPY FSA+1
 BCC HIDECE
 BNE HIDECX
 CMP FSA
 BCC HIDECE
HIDECX RTS
 JSR SETFSA;HIDECE-3
HIDECE JMP ALLOCR
TOKOUT STA WORK
 CMPIM &80
 BCC CHOUT
 LDAIM TOKENS
 STA WORK+1
 LDAIM /TOKENS
 STA WORK+2
 PHY
FINTOK LDYIM 0
LOOTOK INY
 LDAIY WORK+1
 BPL LOOTOK
 CMP WORK
 BEQ GOTTOK
 INY
 TYA
 SEC
 ADC WORK+1
 STA WORK+1
 BCC FINTOK
 INC WORK+2
 BRA FINTOK
GOTTOK LDYIM 0
PRTTOK LDAIY WORK+1
 BMI ENDTOK
 JSR CHOUT
 INY
 BNE PRTTOK
ENDTOK PLY
 RTS
HEXOUT PHA
 LSRA
 LSRA
 LSRA
 LSRA
 JSR DIG
 PLA
 ANDIM &0F
DIG CMPIM &0A
 BCC DIGR
 ADCIM 6
DIGR ADCIM &30
NCH PHA
 LDA WIDTHV
 CMP TALLY
 BCS NOCRLF
 JSR NLINE
NOCRLF PLA
 INC TALLY
 JMI &20E
HEXSP JSR HEXOUT
LISTPT LDAIM " "
CHOUT BIT LISTOP
 BMI CHOUTR
 CMPIM &0D ;CHOUT+4 also an entry point
 BNE NCH
 JSR OSWRCH
 JMP BUFEND
CHOUTR STAI FSA
 INC FSA
 BNE LISTPX
 INC FSA+1
 PHA
 LDA FSA+1
 EOR HIMEM+1
 BEQ HIDECE-3;no room and clear
 PLA
 RTS
 CLC ;LISTPS-1
LISTPS AND LISTOP
 BEQ LISTPX
 TXA
 BMI LISTPX
 ROLA
 TAX
 BEQ LISTPX
LISTPL JSR LISTPT
 DEX
 BNE LISTPL
LISTPX RTS
ACCTOM LDA IACCL
 STAAX 0
 LDA IACCM
 STAAX 1
 LDA IACCN
 STAAX 2
 LDA IACCH
 STAAX 3
 RTS
LOADER JSR OSTHIG
 CLR WORK+6
 LDYIM 0
 LDAIM &FF
 LDXIM WORK
 JSR OSFILE
ENDER LDA TXTP
 STA TOP+1
 CLR TOP
 LDYIM 1
FNDTOP LDAI TOP
 CMPIM &0D
 BNE BADPRO
 LDAIY TOP
 BMI SETTOP
 LDYIM 3
 LDAIY TOP
 BEQ BADPRO
 CLC
 JSR YADT+1
 BRA FNDTOP
SETTOP INY
CLYADT CLC
YADT TYA
 ADC TOP
 STA TOP
 BCC ENDADT
 INC TOP+1
ENDADT LDYIM 1
 RTS
BADPRO JSR VSTRNG
 = 13,"Bad program",13
 NOP
 JMP CLRSTK
OSSTRG CLR WORK ;STRACC is 0
 LDAIM /STRACC
 STA WORK+1
OSSTRT LDY CLEN
 LDAIM &0D
 STAAY STRACC
 RTS
OSTHIE JMP LETM
OSTHIF JSR AEEXPR
 BNE OSTHIE
 JSR OSSTRG
 JMP FDONE
OSTHIG JSR OSTHIF
 DEY
 STY WORK+2
 LDA TXTP
 STA WORK+3
GETMAC LDAIM &82
 JSR OSBYTE
 STX WORK+4
 STY WORK+5
 RTS
SAVE JSR ENDER
 JSR OSTHIG
 STX WORK+8
 STY WORK+9
 STX WORK+12
 STY WORK+13
 STX WORK+16
 STY WORK+17
 CLR WORK+10
 LDX TOP
 STX WORK+14
 LDX TOP+1
 STX WORK+15
 LDXIM ENTRY
 STX WORK+6
 LDXIM /ENTRY
 STX WORK+7
 LDX TXTP
 STX WORK+11
 LDAIM 0
 TAY
 LDXIM WORK
 JSR OSFILE
 BRA LPTRX
OSCL JSR OSTHIF
 LDXIM STRACC
 LDYIM /STRACC
 JSR OSCLI
 BRA LPTRX
SETEXT LDAIM 3
 BRA LPTR+2
LPTR LDAIM 1
 PHA
 JSR AECHAN
 PHY
 JSR EQEXPR
 JSR INTEG
 PLY
 LDXIM IACCL
 PLA
 JSR OSARGS
LPTRX JMP NXT
CLOSE JSR AECHAN
 JSR AEDONE
 LDY IACCL
 LDAIM 0
 JSR OSFIND
 BRA LPTRX
BPUT JSR AECHAN
 PHA
 JSR INCMEX
 JSR AEDONE
 PLY
 LDA IACCL
 JSR OSBPUT
 BRA LPTRX
VSTRNG PLA
 STA WORK
 PLA
 STA WORK+1
 BRA VSTRLP
VSTRLM JSR OSASCI
VSTRLP JSR GETWK2
 BPL VSTRLM
 JMI WORK
READBYTE LDAIM 5
 PHX
 LDXIM IACCL
 LDYIM 0
 JSR OSWORD
 PLX
 LDA IACCL+4
INCACC INC IACCL
 BNE INCDON
 INC IACCM
 BNE INCDON
 INC IACCN
 BNE INCDON
 INC IACCH
INCDON RTS
FROMAT LDAIM &0D ;clear text by 0d ff at start
 LDY TXTP
 STY TOP+1
 CLR TOP
 CLR TRFLAG
 STAI TOP
 LDAIM &FF
 LDYIM 1
 STAIY TOP
 INY
 STY TOP
 RTS
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
 LDA &400
VECTAB & FSQRT
 & FXDIV
 & FMUL
 & FADD
 & FNEG
 & FLDA
 & FSTA
 = ARGP,FACCS
RPLN10 = &7F,&5E,&5B,&D8,&AA ;4.342944820E-1
 = 0,&CA,&98,&80 ;MA & X of 2^23 & PI/4
TRIGCO = &81,&22,&F9,&83,&6E ;0.6366197723*2=4/PI
HALFPI = &81,&49,&0F,&DA,&A2,&21 ;1.570796327, 6 bytes
 = &B3,&B2,&87,&80 ;MA & X of 89.5 & LN(2)
EXPCOF = &82,&38,&AA,&3B,&29 ; 2.885390081=2/LN2
LOGTWO = &80,&31,&72,&17,&F7,&D1 ;.6931471806E =LN2 ,6 bytes
 = &5F,&5B,&E6,&FF ; 10^-10 to 4 bytes
 = &66,&2B,&CC,&77 ; 10^-8 ditto
 = &6D,6,&37,&BD ;10^-6 ditto
 = &73,&51,&B7,&17 ;10^-4 ditto
 = &7A,&23,&D7,&A ;10^-2 ditto
ONEP0 = &81,0,0,0,0 ;1.0
F3P0 = &82,&40,0,0,0 ;3.0
FATANC = &9A,&D4,&82,&7F ;MA & X of 2.414 & .414
 = 185,&FF,&78 ;test sizes for FCFTAN
TANCON
FPID18 = &7B,&0E,&FA,&35,&12 ;1.745329252E-2
F180DP = &86,&65,&2E,&E0,&D3 ;5.729577951E1
; these 2 constants set after TANCON as biggest cycle is 2
 = &7E,&88,&88,&88,&89 ;-.1333=-3*2/45
 = &7B,&8C,&6F,&2D,&59 ;-9*1/(3*5*5*7)
 = &81,&99,&99,&99,&9A ;-1.2=-3*2/5
 = 243,158,&7B,&77 ;test sizes for FCFATN
ATNCON = &81,&C0,0,0,0 ;-3*59/117 modified to -3/2
 = &80,&93,&E6,&90,0  ;-9*(4*5/9)^2/(7*11) mod. to -9*.064193
 = &81,&C4,&44,&44,&44 ;-3*23/45
 = &80,&9D,&FD,&13,4 ;-9*36/(3*5*5*7)
 = &81,&E6,&66,&66,&66 ;-3*3/5
ZEEND
 ASSERT :MSB: . = :MSB: RPLN10
 = "Roger Wilson & R.A.Sack"
 WHILE :LSB: . <> 0
 BRK
 WEND
 END
